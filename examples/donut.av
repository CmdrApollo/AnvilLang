var bufSize: int = 80 * 50;
var characters: string = ".,-~:;=!*#$@";

var a: float = 1.0;
var b: float = 1.0;

fn clamp(value: float, minValue: float, maxValue: float) -> float {
    return min(max(value, minValue), maxValue);
}

var zBuffer: arr<float> = [];
var output: arr<string> = [];

loop (unpack(bufSize)) -> (_: int) {
    zBuffer = append(zBuffer, 0.0);
    output = append(output, " ");
}

println("\e[?25l");

while (true) {
    loop (unpack(bufSize)) -> (o: int) {
        zBuffer[o] = 0.0;
        output[o] = " ";
    }

    a = a + 0.07;
    b = b + 0.03;

    var sinA: float = mathSin(a);
    var cosA: float = mathCos(a);
    var sinB: float = mathSin(b);
    var cosB: float = mathCos(b);

    var j: float = 0.0;
    while (j < 6.28) {
        var sinJ: float = mathSin(j);
        var cosJ: float = mathCos(j);

        var i: float = 0.0;
        while (i < 6.28) {
            var sinI: float = mathSin(i);
            var cosI: float = mathCos(i);

            var h: float = cosJ + 2.0;
            var d: float = 1.0 / (sinI * h * sinA + sinJ * cosA + 5.0);
            var t: float = sinI * h * cosA - sinJ * sinA;

            var x: int = mathFloor(clamp(40 + 30 * d * (cosI * h * cosB - t * sinB), 0.0, 79.0));
            var y: int = mathFloor(clamp(25 + 15 * d * (cosI * h * sinB + t * cosB), 0.0, 49.0));
            var o: int = y * 80 + x;

            if (d > zBuffer[o]) {
                zBuffer[o] = d; 

                var characterIndex: int = mathFloor(max(0, 8 * ((sinJ * sinA - sinI * cosJ * cosA) * cosB - sinI * cosJ * sinA - sinJ * cosA - cosI * cosJ * sinB)));
                characterIndex = mathFloor(min(11, max(0, characterIndex)));

                output[o] = characters[characterIndex];
            }

            i = i + 0.02;
        }

        j = j + 0.07;
    }

    println("\e[H");

    loop (unpack(bufSize)) -> (idx: int) {
        print(output[idx]);
        if ((idx + 1) % 80 == 0) {
            println("");
        }
    }
}